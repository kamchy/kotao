buildscript {
    ext.kotlin_version = '1.1.2-2'
    ext.jackson_version = '2.8.3'
    ext.vertx_version = '3.4.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "io.github.knes1.kotao.brew.ApplicationKt"
version = '0.1.1'

jar {
    baseName = 'kotao'
    version =  project.version
    manifest {
        attributes 'Implementation-Title': 'Kotao Static Site Generator',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
}

/**
 * Unzips the distribution zip.
 */
task install(type: Copy, dependsOn: distZip) {

    println("Starting install")
    def zipFile = file("${buildDir}/distributions/kotao-${project.version}.zip")
    def outputDir = file("${buildDir}/distributions/")

    from zipTree(zipFile)
    into outputDir

    doLast {
        def output = "${outputDir}/kotao"
        //cleanup any previous installations
        if (file(output).exists()) {
            file(output).deleteDir()
        }
        file("${outputDir}/kotao-${project.version}").renameTo("${outputDir}/kotao")
        println("Kotao installation available at ${outputDir}/kotao")
    }

}

/**
 * Adds version.properties with current build version to project.
 */
task createVerProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version
            p.store w, null
        }
    }
}

processResources.finalizedBy(createVerProperties)

repositories {
    jcenter()
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'

    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"

    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile "com.xenomachina:kotlin-argparser:2.0.1"

    //FreeMarker
    compile 'org.freemarker:freemarker:2.3.24-incubating'

    //Markdown
    compile group: 'com.vladsch.flexmark', name: 'flexmark', version: '0.19.6'

    //Slugify
    compile 'com.github.slugify:slugify:2.1.5'

    //Mongo
    compile 'org.mongodb:mongo-java-driver:3.0.0'
    compile 'org.jongo:jongo:1.2'

    //YUI Compressor
    compile group: 'com.yahoo.platform.yui', name: 'yuicompressor', version: '2.4.8'

    //Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.7'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'

    // Filewatcher that works without bugs on OS/X
    compile group: 'io.reactivex', name: 'rxjava-file-utils', version: '0.1.4'


    //Web Server
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'junit:junit'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
